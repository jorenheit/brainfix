// STD IO

/* Provided IO functions:

   OUTPUT

   printc(x)        - Print byte as ASCII character
   printd(x)        - Print byte as decimal (max 3 digits)
   printd_4(x)      - Print byte as decimal (max 4 digits)
   prints(str)      - Print string (stop at NULL or end of the string)
   println(str)     - Same as prints() but with appended newline
   print_vec(v)     - Print formatted vector, including newline: (v1, v2, v3, ..., vn)
   endl()           - Print a newline (same as printc('\n'))
   
   INPUT

   scanc()          - Read a single byte from stdin
   scand()          - Read at most 3 bytes from stdin and convert to decimal (0-999)
   scand_4()        - Read at most 4 bytes from stdin and convert to decimal (0-9999)
   scans(buf)       - Read string from stdin. sizeof(buf) determines maximum number of bytes read
   
   CONVERSIONS

   to_int(str)      - Converts string to int, max 3 digits (0-999)
   to_int_4(str)    - Converts string to int, max 4 digits (0-9999)
   to_string(x)     - Converts int to string, max 3 digits (0-999)
   to_string_4(x)   - Converts int to string, max 4 digits (0-9999)
   to_binary_str(x) - Converts number to 8-bit binary representation (as string)
   to_hex_str(x)    - Converts number to 8-bit hexadecimal representation (as string)
*/

function printc(&c)
{
    __print(c);
}

function prints(&str)
{
    let i = 0;
    while (i < sizeof(str) && str[i] != 0)
        printc(str[i++]);    
}

function println(&x)
{
    prints(x);
    endl();
}

function printd(&x)
{
    let [3] str = to_string(x);
    for (let &d: str)
        if (d) printc(d);
}

function printb(b)
{
    if (b)
        prints("true");
    else
        prints("false");
}

function printd_4(&x)
{
    let [4] str = to_string_4(x);
    for (let d: str)
        if (d) printc(d);
}


function print_vec(&v)
{
    printc('(');
    for(let i = 0; i != sizeof(v); ++i)
    {
        printd(v[i]);
        if (i != sizeof(v) - 1)
        {
            printc(','); 
            printc(' ');
        }
    }
    printc(')');
    endl();
}

function endl()
{
    printc('\n');
}

function c = scanc()
{
    let c = __scan();
}

function result = scand()
{
    let [3] input = 0;
    scans(input);

    let [3] powers = #{1, 10, 100};
    let result = 0;
    let j = 0;
    
    for (let i = 0; i != 3; ++i)
    {
        let x = input[2 - i];
        if (x != 0)
            result += (x - '0') * powers[j++];
    }
}

function result = scand_4()
{
    let [4] input = 0;
    scans(input);

    let [4] powers = #{1, 10, 100, 1000};
    let result = 0;
    let j = 0;
    
    for (let i = 0; i != 4; ++i)
    {
        let x = input[3 - i];
        if (x != 0)
            result += (x - '0') * powers[j++];
    }
}

function scans(&buf)
{
    let i = 0;
    let done = 0;
    while (!done)
    {
        let c = scanc();
        if (c != '\n')
        {
            if (i < sizeof(buf))
                buf[i++] = c;
        }
        else
        {
            buf[i] = 0;
            done = 1;
        }
    }
}

function result = to_int(&str)
{
    static_assert(sizeof(str) <= 3, "Argument to to_int can have at most 3 digits.");
    
    let nDigits = sizeof(str);
    if (nDigits > 3)
        nDigits = 3;

    let [3] powers = #{1, 10, 100};
    let result = 0;
    let j = 0;
    for (let i = 0; i != nDigits; ++i)
    {
        let x = str[nDigits - 1 - i];
        if (x != 0)
            result += (x - '0') * powers[j++];
    }
}

function result = to_int_4(&str)
{
    static_assert(sizeof(str) <= 3, "Argument to to_int_4 can have at most 4 digits.");

    let nDigits = sizeof(str);
    if (nDigits > 4)
        nDigits = 4;

    let [4] powers = #{1, 10, 100, 1000};
    let result = 0;
    let j = 0;
    for(let i = 0; i != nDigits; ++i)
    {
        let x = str[nDigits - 1 - i];
        if (x != 0)
            result += (x - '0') * powers[j++];
    }
}
    
function str = to_binary_str(x)
{
    let [] powers = #{128, 64, 32, 16, 8, 4, 2, 1};
    let [] str = "00000000";
    for(let i = 0; i < sizeof(powers); ++i)
    {
        let p = powers[i];
        if (x >= p)
        {
            str[i] = '1';
            x -= p;
        }
    }
}

function str = to_hex_str(x)
{
    let lowNibble = (x /=% 16);
    let [] c      = "0123456789abcdef";
    let [] str    = #{'0', 'x', c[x], c[lowNibble]};
}

function str = to_string(x)
{
    let hundreds = (x %=/ 100);
    let tens = (x %=/ 10);
    let ones = x;
    
    let idx = 0;
    let [3]str = 0;
    
    if (hundreds != 0)
    {
        str[idx++] = hundreds + '0';
        str[idx++] = tens + '0';
    }
    else if (tens != 0)
    {
        str[idx++] = tens + '0';
    }

    str[idx] = ones + '0';
}

function str = to_string_4(x)
{
    let thousands = (x %=/ 1000);
    let hundreds = (x %=/ 100);
    let tens = (x %=/ 10);
    let ones = x;

    let idx = 0;
    let [4] str = 0;

    if (thousands != 0)
    {
        str[idx++] = thousands + '0';
        str[idx++] = hundreds + '0';
        str[idx++] = tens + '0';
    }
    else if (hundreds != 0)
    {
        str[idx++] = hundreds + '0';
        str[idx++] = tens + '0';
    }
    else if (tens != 0)
    {
        str[idx++] = tens + '0';
    }

    str[idx] = ones + '0';
}

function clear_screen()
{
    printc(27);
    prints("[H");
    printc(27);
    prints("[2J");
}

function clear_line()
{
    printc(13);
    printc(27);
    prints("[0K");
}

function cursor_to(&line, &column)
{
    printc(27);
    printc('[');
    prints(to_string(line));
    printc(';');
    prints(to_string(column));
    printc('H');
}

function cursor_up(n)
{
    printc(27);
    printc('[');
    prints(to_string(n));
    printc('A');
}

function cursor_up()
{
    printc(27);
    prints("[1A");
}

function cursor_down(n)
{
    printc(27);
    printc('[');
    prints(to_string(n));
    printc('B');
}

function cursor_down()
{
    printc(27);
    prints("[1B");
}

function cursor_right(n)
{
    printc(27);
    printc('[');
    prints(to_string(n));
    printc('C');
}

function cursor_right()
{
    printc(27);
    prints("[1C");
}

function cursor_left(n)
{
    printc(27);
    printc('[');
    prints(to_string(n));
    printc('D');
}

function cursor_left()
{
    printc(27);
    prints("[1D");
}
